apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'pmd'

// Invoke ./gradlew dependencyCheckAnalyze to conduct security analysis
dependencyCheck {
    autoUpdate=true
    cveValidForHours=24
    format='HTML'
}

// Invoke ./gradlew check to run code quality assessements
pmd {
    toolVersion = '5.5.5'
    ignoreFailures = false
    sourceSets = [sourceSets.main]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets =  ["java-basic", "java-braces", "java-j2ee", "java-naming", "java-imports", "java-sunsecure", "java-unusedcode", "java-unnecessary","java-codesize"]
    rulePriority = 3
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'org.owasp:dependency-check-gradle:3.2.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven {
        url 'https://maven.google.com'
    }
}

dependencies {
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:support-v13:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:appcompat-v7:26.1.0'
}

// The sample build uses multiple directories to
// keep boilerplate and common code separate from
// the main sample code.
List<String> dirs = [
    'main',     // main sample code; look here for the interesting stuff.
    'common',   // components that are reused by multiple samples
    'template'] // boilerplate code that is generated by the sample template process

android {
    signingConfigs {
        config {
            keyAlias 'debug'
            keyPassword 'debug'
            storePassword 'debug'
        }
    }
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "org.able.core"
        minSdkVersion 18
        targetSdkVersion 26
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main {
            dirs.each { dir ->
                java.srcDirs "src/${dir}/java"
                res.srcDirs "src/${dir}/res"
            }
        }
        androidTest.setRoot('tests')
        androidTest.java.srcDirs = ['tests/src']

    }
}
